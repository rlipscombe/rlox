use std::str::FromStr;
use crate::ast::*;

grammar;

pub Expr: Box<Expr> = {
    <l:Expr> <o:CompareOp> <r:Comparison> => Box::new(Expr::Binary(l, o, r)),
    Comparison
}

CompareOp: BinaryOp = {
    "<" => BinaryOp::Lt,
    "<=" => BinaryOp::Le,
    ">" => BinaryOp::Gt,
    ">=" => BinaryOp::Ge,
}

pub Comparison: Box<Expr> = {
    <l:Comparison> <o:AddOp> <r:Factor> => Box::new(Expr::Binary(l, o, r)),
    Factor,
}

AddOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
}

Factor: Box<Expr> = {
    <l:Factor> <o:FactorOp> <r:Term> => Box::new(Expr::Binary(l, o, r)),
    Term,
}

FactorOp: BinaryOp = {
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
}

Term: Box<Expr> = {
    Num => Box::new(Expr::Number(<>)),
    Bool => Box::new(Expr::Boolean(<>)),
    "nil" => Box::new(Expr::Nil),
    "-" <n:Num> => Box::new(Expr::Number(-n)),
    "(" <Expr> ")",
};

Num: f64 = r"[0-9]+(\.[0-9]+)?" => f64::from_str(<>).unwrap();
Bool: bool = {
    "true" => true,
    "false" => false,
}
